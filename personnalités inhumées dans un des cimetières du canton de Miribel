# personnalités enterrées dans un des cimetières des cantons de Miribel et de Meximieux 
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?item ?itemLabel ?lieu ?lieuLabel ?image

WHERE {
	
  ?item wdt:P31/wdt:P279* wd:Q5 . #humain
  
  ?item wdt:P119 ?lieu . #lieu de sépulture
  
  ?lieu wdt:P31/wdt:P279* wd:Q39614 . # lieu de sépulture est un cimetière.

  ?lieu wdt:P131 ?commune .
  ?commune wdt:P31/wdt:P279* wd:Q484170 . # c'est bien une commune.
  { ?commune wdt:P131 wd:Q735692 . } # elle est dans le canton de Miribel.
  UNION
  { ?commune wdt:P131 wd:Q1553619 . } # ou dans le canton de Meximieux.
  
  OPTIONAL{ ?item wdt:P1442 ?image . }
  
  SERVICE wikibase:label { bd:serviceParam wikibase:language "fr". }
  
} ORDER BY DESC(?lieuLabel)
