# Points culminants par département français, collectivité d'Outre-mer

PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?item ?itemLabel ?culminant ?culminantLabel ?altitude ?partieDeLabel ?lat ?lon ?location
WHERE {

  { ?item wdt:P31/wdt:P279* wd:Q6465. } 
  UNION
  { ?item wdt:P31/wdt:P279* wd:Q719487. } 

  ?item p:P31 ?dep .
  FILTER NOT EXISTS { ?item p:P576 ?x } 
  FILTER NOT EXISTS { ?item p:P582 ?x } 
  FILTER NOT EXISTS { ?dep pq:P582 ?x }  
  OPTIONAL{
    ?item wdt:P610 ?culminant  .
    OPTIONAL{ ?culminant wdt:P2044 ?altitude . }
    OPTIONAL{ ?culminant wdt:P361 ?partieDe . }
    OPTIONAL{ ?culminant wdt:P625 ?location . }
    OPTIONAL{ ?culminant p:P625 ?coordinate . 
    ?coordinate psv:P625 ?coordinate_node . 
    ?coordinate_node wikibase:geoLatitude ?lat .  
    ?coordinate_node wikibase:geoLongitude ?lon . }
  }

  SERVICE wikibase:label { bd:serviceParam wikibase:language "fr". }
  
} ORDER BY DESC(?altitude)
